# ===============================================================
# DynDNS Update (NAT) using MikroTik Cloud Public IP
# No file writing, uses only global variable for last IP
# Made & Powered by Thanos Pournaras | https://thanosnm.github.io
# ===============================================================

:global ddnsUser "myuser"
:global ddnsPass "mypassword123"
:global ddnsHost "myhome.dyndns.org"
:global ddnsLastIP

# Get public IP from MikroTik Cloud
:local currentIP [/ip cloud get public-address]
:if ([:find $currentIP ":"] > 0) do={
    :set currentIP [:pick $currentIP 0 [:find $currentIP ":"]]
}

# Resolve DynDNS current IP
:local ipddns [:resolve $ddnsHost]

# If IP changed
:if ($ipddns != $currentIP || $ddnsLastIP != $currentIP) do={

    :log info ("DynDNS: Updating to " . $currentIP)

    :local url ("http://" . $ddnsUser . ":" . $ddnsPass . "@members.dyndns.org/nic/update?hostname=" . $ddnsHost . "&myip=" . $currentIP . "&wildcard=NOCHG&mx=NOCHG&backmx=NOCHG")

    /tool fetch url=$url mode=http keep-result=no

    :set ddnsLastIP $currentIP
    :log info "DynDNS: Update done."

} else={
    :log info ("DynDNS: No update needed. IP is still " . $currentIP)
}
